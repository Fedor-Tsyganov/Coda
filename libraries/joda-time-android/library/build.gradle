apply plugin: 'android-library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    sourceSets {
        main.manifest.srcFile 'AndroidManifest.xml'
        main.java.srcDir 'src'
        main.res.srcDir 'res'
        main.assets.srcDir 'assets'
        androidTest.setRoot('tests')
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

apply from: 'gradle-mvn-push.gradle'

repositories {
    mavenCentral()
}

configurations {
    updateTzData
}

dependencies {
    compile 'joda-time:joda-time:2.7:no-tzdb'
    updateTzData 'joda-time:joda-time:2.7'
}

task updateTzData(type: JavaExec) {
    classpath configurations.updateTzData + files("build/intermediates/bundles/debug/classes.jar")
    main = "net.danlew.android.joda.ZoneInfoCompiler"
    systemProperty "org.joda.time.DateTimeZone.Provider", "org.joda.time.tz.UTCProvider"
    args "-src", "../tzdata", "-dst", "res/raw", "africa",  "antarctica", "asia", "australasia", "europe", "northamerica", "southamerica", "pacificnew", "etcetera", "backward", "systemv"
}
